buildscript {
    ext.kotlin_version = '1.1.51'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins { id "com.jfrog.bintray" version "1.2" }


apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'maven'
apply plugin: 'maven-publish'

//mainClassName = 'demo.HelloWorldKt'
mainClassName = "de.mpicbg.scicomp.kutils.BashKt" // not needed but does not work without

defaultTasks 'run'

repositories {
    mavenCentral()
    mavenLocal()
}

// https://docs.gradle.org/current/userguide/java_plugin.html#sec:java_plugin_and_dependency_management
dependencies {
//    compileOnly "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "com.offbytwo:docopt:0.6.0.20150202"

    testCompile 'junit:junit:4.11'
    testCompile "io.kotlintest:kotlintest:2.0.1"
}

// http://stackoverflow.com/questions/11474729/how-to-build-sources-jar-with-gradle
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

//http://stackoverflow.com/questions/34377367/why-is-gradle-install-replacing-my-version-with-unspecified
//group = 'de.mpicbg.scicomp'
group = 'com.github.holgerbrandl'
version = '1.2.4'


artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar { classifier "sources" }

            // just needed because artifact name differs from project name
            version = project.version
            artifactId = 'kscript-support'
            groupId = project.group
        }
    }
}


install {
    repositories.mavenInstaller {
        pom.version = project.version
        pom.artifactId = 'kscript-support'
        pom.groupId = project.group
    }
}

if (hasProperty('bintray_user') && hasProperty('bintray_key')) {
    bintray {

        // property must be set in ~/.gradle/gradle.properties
        user = bintray_user
        key = bintray_key

        publications = ['maven'] //When uploading configuration files

        dryRun = false //Whether to run this as dry-run, without deploying
        publish = true // If version should be auto published after an upload

        pkg {
            repo = 'github'
            name = 'kscript-support'
            vcsUrl = 'https://github.com/holgerbrandl/kscript-support-api'

            licenses = ['MIT']
            publicDownloadNumbers = true

            //Optional version descriptor
            version {
                name = project.version //Bintray logical version name
                desc = '.'
                released = new java.util.Date()
                vcsTag = 'v' + project.version
            }
        }
        /**/
    }
}
